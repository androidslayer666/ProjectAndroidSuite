// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.21"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.28-alpha"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.3.5"

    }
}

ext.sharedGroupLifecycle = { dependencyHandler ->
    delegate = dependencyHandler

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
}


ext.sharedGroupHilt = {dependencyHandler->
    delegate = dependencyHandler

    def hilt_version = "2.36"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

}

ext.sharedGroupNetwork = { dependencyHandler ->
    delegate = dependencyHandler

    def retrofit_version = "2.6.0"
    api "com.squareup.retrofit2:retrofit:$retrofit_version"
    api "com.squareup.retrofit2:converter-gson:$retrofit_version"
    api 'com.squareup.okhttp3:logging-interceptor:3.12.0'

    implementation "com.squareup.retrofit2:converter-moshi:2.4.0"
}

ext.sharedGroupRoom= { dependencyHandler ->
    delegate = dependencyHandler

    def room_version = "2.3.0"
    api "androidx.room:room-runtime:$room_version"
    api "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}